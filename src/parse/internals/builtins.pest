// Main grammars
/// The debug builtin.
builtinDbg = !{ "dbg" ~ (space+ ~ elviWord)* }

/// The unset builtin.
builtinUnset = !{ "unset" ~ (space+ ~ elviWord)* }

/// The exit builtin.
builtinExit = !{ "exit" ~ (space+ ~ elviWord)* }

/// The hash builtin.
builtinHash = !{ "hash" ~ (space+ ~ elviWord)* }

/// The cd builtin.
builtinCd = !{ "cd" ~ (space+ ~ elviWord)* }
// TODO: Add commands to `builtinTest` and also `-a` and `-o`
/// The test builtin.
builtinTest = !{ ("[" ~ space+ ~ (#invert = builtinTestInvert ~ space+)? ~ (builtinTestPrimaries | builtinTestComparisons) ~ space+ ~ "]") | ("test" ~ space+ ~ (builtinTestInvert ~ space+)? ~ (builtinTestPrimaries | builtinTestComparisons)) }

/// The echo builtin.
builtinEcho = !{ "echo" ~ (space+ ~ elviWord)* }

/// The invert symbol (`!`).
builtinTestInvert = { "!" }

/// `test` primaries just means a flag that takes an input, such as `-f foo`.
builtinTestPrimaries = {
    #block = ("-b" ~ space+ ~ elviWord)
  | #character_special = ("-c" ~ space+ ~ elviWord)
  | #directory_exists = ("-d" ~ space+ ~ elviWord)
  | #file_exists = ("-e" ~ space+ ~ elviWord)
  | #regular_file_exists = ("-f" ~ space+ ~ elviWord)
  | #file_exists_group_id = ("-g" ~ space+ ~ elviWord)
  | #symbolic_link = (("-h" | "-L") ~ space+ ~ elviWord)
  | #sticky_bit_set = ("-k" ~ space+ ~ elviWord)
  | #string_nonzero = ("-n" ~ space+ ~ elviWord)
  | #named_pipe = ("-p" ~ space+ ~ elviWord)
  | #readable_file = ("-r" ~ space+ ~ elviWord)
  | #greater_than_zero_file = ("-s" ~ space+ ~ elviWord)
  | #file_descriptor = ("-t" ~ space+ ~ elviWord)
  | #file_exists_user_id = ("-u" ~ space+ ~ elviWord)
  | #writable_file = ("-w" ~ space+ ~ elviWord)
  | #executable_file = ("-x" ~ space+ ~ elviWord)
  | #string_zero = ("-z" ~ space+ ~ elviWord)
  | #efective_user_id_file = ("-O" ~ space+ ~ elviWord)
  | #efective_group_id_file = ("-G" ~ space+ ~ elviWord)
  | #socket_file_exists = ("-S" ~ space+ ~ elviWord)
}

/// `test` comparisons are two inputs that are operated on into a boolean.
builtinTestComparisons = {
    #string_equals = (elviWord ~ space+ ~ "=" ~ space+ ~ elviWord)
  | #string_not_equals = (elviWord ~ space+ ~ "!=" ~ space+ ~ elviWord)
  | #ascii_comparison_lt = (elviWord ~ space+ ~ "<" ~ space+ ~ elviWord)
  | #ascii_comparison_gt = (elviWord ~ space+ ~ ">" ~ space+ ~ elviWord)
  | #integer_eq = (elviWord ~ space+ ~ "-eq" ~ space+ ~ elviWord)
  | #integer_ne = (elviWord ~ space+ ~ "-ne" ~ space+ ~ elviWord)
  | #integer_gt = (elviWord ~ space+ ~ "-gt" ~ space+ ~ elviWord)
  | #integer_ge = (elviWord ~ space+ ~ "-ge" ~ space+ ~ elviWord)
  | #integer_lt = (elviWord ~ space+ ~ "-lt" ~ space+ ~ elviWord)
  | #integer_le = (elviWord ~ space+ ~ "-le" ~ space+ ~ elviWord)
}

/// Wrapper grammar for any builtin.
builtinWrapper = { builtinDbg | builtinUnset | builtinExit | builtinHash | builtinCd | builtinTest | builtinEcho }
